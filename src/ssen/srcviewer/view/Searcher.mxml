<?xml version="1.0" encoding="utf-8"?>
<s:TitleWindow xmlns:fx="http://ns.adobe.com/mxml/2009" xmlns:s="library://ns.adobe.com/flex/spark" width="320" height="600"
			   close="closeThis()" title="Search" creationComplete="init(event)" alpha="0.5">
	<s:layout>
		<s:VerticalLayout paddingTop="10" paddingBottom="10" paddingLeft="10" paddingRight="10" gap="10" horizontalAlign="left"
						  verticalAlign="top"/>
	</s:layout>

	<fx:Script>
		<![CDATA[
			import mx.collections.ArrayList;

			import spark.events.IndexChangeEvent;
			import spark.events.TextOperationEvent;

			import ssen.mvc.IEventBus;
			import ssen.srcviewer.model.Doc;
			import ssen.srcviewer.model.DocEvent;
			import ssen.srcviewer.model.DocModel;

			[Inject]
			public var eventBus:IEventBus;

			[Inject]
			public var docmodel:DocModel;

			[Bindable]
			public var list:ArrayList=new ArrayList;

			private function type(event:TextOperationEvent):void {
				var result:Vector.<Doc>=docmodel.searchDoc(input.text);

				list.removeAll();

				var f:int=-1;
				var fmax:int=result.length;
				while (++f < fmax) {
					list.addItem(result[f]);
				}
			}

			private function closeThis():void {
				stage.removeEventListener(KeyboardEvent.KEY_UP, escapeWindow);
				eventBus.dispatchEvent(new ViewEvent(ViewEvent.CLOSE_SEARCHER));
			}

			private function selectDoc(event:IndexChangeEvent):void {
				eventBus.dispatchEvent(new DocEvent(DocEvent.OPEN_DOC, result.selectedItem));
			}

			private function init(event:Event):void {
				focusManager.setFocus(input);
				stage.addEventListener(KeyboardEvent.KEY_UP, escapeWindow);
			}

			private function moveFocusToList(event:KeyboardEvent):void {
				if (event.keyCode === Keyboard.DOWN) {
					focusManager.setFocus(result);
				}
			}

			private function moveFocusToInput(event:KeyboardEvent):void {
				if (event.keyCode === Keyboard.UP && result.selectedIndex === 0) {
					focusManager.setFocus(input);
				}
			}

			private function escapeWindow(event:KeyboardEvent):void {
				if (event.keyCode === Keyboard.ESCAPE) {
					closeThis();
				}
			}
		]]>
	</fx:Script>

	<s:TextInput id="input" width="100%" change="type(event)" keyUp="moveFocusToList(event)"/>
	<s:List id="result" width="100%" height="100%" dataProvider="{list}" labelField="namespace" change="selectDoc(event)"
			keyUp="moveFocusToInput(event)"/>
</s:TitleWindow>

<!DOCTYPE html><html><head><link href="prism.css" rel="stylesheet" /><script src="marked.js" type="application/javascript"></script></head><body><h1>Day.md</h1><script type="text/markdown" to="cab512ac913">달력 등을 만들때 날짜 계산과 같은 작업을 쉽게 하고, 시간이 포함된 Date 를 좀 더 편하게 사용하기 위해서 만든 단위  

[example](DayExample.as)

# 제공하는 API

- 비교
	- `equal()`
	- `equalMonth()`
	- `equalWeek()`
	
- 정보
	- `weekOfMonth`
	- `weekOfYear`
	- `dayOfYear`
	
- 복제, 이동
	- `clone()`
	- `getNextDay()`
	- `getPrevDay()`
	- `getFirstDayOfMonth()`
	- `getLastDayOfMonth()`
	- `getFirstDayOfWeek()`
	- `getLastDayOfWeek()`
	- `getNextMonth()`
	- `getPrevMonth()`
	- `getNextWeek()`
	- `getPrevWeek()`
	- `getNextYear()`
	- `getPrevYear()`</script><div class="markdown" id="cab512ac913"></div><h1>Day__Example.as</h1><pre><code class="language-javascript">/// Example
package ssen.common.date {
import de.polygonal.ds.Array2;

public class Day__Example {

	// 일년 중 몇 번째 날인지 확인
	[Test]
	public function dayOfYear():void {
		var t:Date=new Date;
		var d1:Date=new Date(t.fullYear, 0, 1);
		var day:Day=new Day(d1);

		var f:int=-1;
		var fmax:int=100;
		while (++f < fmax) {
			trace("DayTest.dayOfYear()", day, day.dayOfYear, day.weekOfMonth, day.weekOfYear);
			day=day.getNextDay();
		}
	}

	// 월 달력을 위한 2차원 XY Grid 를 만든다
	[Test]
	public function createMonthlyGrid():void {
		// 이번달의 첫째날을 찾는다
		var day:Day=new Day().getFirstDayOfMonth();

		// 이번달의 마지막날을 찾는다
		var last:Day=new Day().getLastDayOfMonth();

		// 달력의 최소 가능 그리드 7 x 4 생성
		var arr:Array2=new Array2(7, 4);

		// x index 를 요일로 맞춘다
		var x:int=day.date.day;
		var y:int=0;

		// 이번달이 아니게 될 때까지 반복
		while (day.equalMonth(last)) {
			trace("DayTest.createMonthlyGrid()", day.weekOfMonth, day.dayOfYear);
			arr.set(x, y, day);

			day=day.getNextDay();
			x++;

			if (x > 6) {
				x=0;
				y++;

				if (y + 1 > arr.getH()) {
					arr.setH(y + 1);
				}
			}

		}

		trace("DayTest.getMonthlyGrid()", arr);
	}

	// 일주일을 그리기 위한 배열을 만든다
	[Test]
	public function createWeeklyGrid():void {
		// 이번주의 첫째날을 찾는다
		var day:Day=new Day().getFirstDayOfWeek();

		// 7일치를 증가 반복
		var f:int=-1;
		var fmax:int=7;
		var arr:Vector.<Day>=new Vector.<Day>(fmax, true);

		while (++f < fmax) {
			arr[f]=day;
			day=day.getNextDay();
		}

		trace("DayExample.createWeeklyGrid()", arr);

	}
}
}
</code></pre><h1>Day.as</h1><pre><code class="language-javascript">package ssen.common.date {
import ssen.common.StringUtils;


/**
 * 시간을 제외한 날짜 계산을 할 때 사용
 * 
 * @includeExample DayExample.as
 */
public class Day {
	private var d:Date;

	public function Day(date:Date=null) {
		this.date=date;
	}

	/** 현재 날짜 */
	public function get date():Date {
		return d;
	}

	public function set date(date:Date):void {
		d=(date !== null) ? DateUtils.dropTimes(date) : DateUtils.dropTimes(new Date);
	}

	//----------------------------------------------------------------
	// equal
	//----------------------------------------------------------------
	/** 같은 날 체크 */
	public function equal(day:Day):Boolean {
		return day.d.time === d.time;
	}

	/** 같은 달 체크 */
	public function equalMonth(day:Day):Boolean {
		return day.d.fullYear === d.fullYear && day.d.month === d.month;
	}

	/** 같은 주 체크 */
	public function equalWeek(day:Day):Boolean {
		return getFirstDayOfWeek().equal(day.getFirstDayOfWeek());
	}

	//----------------------------------------------------------------
	// info
	//----------------------------------------------------------------
	/** 이번달의 몇째주 */
	public function get weekOfMonth():int {
		return ((d.date + getFirstDayOfMonth().d.day - 1) / 7) + 1;
	}

	/** 올해의 몇째주 */
	public function get weekOfYear():int {
		var firstDay:Date=new Date(d.fullYear, 0, 1);
		var dayOf:int=((d.time - firstDay.time) / Microtimes.DAY) + 1;
		return ((dayOf + firstDay.day - 1) / 7) + 1;
	}

	/** 올해의 몇째일 */
	public function get dayOfYear():int {
		var firstDay:Date=new Date(d.fullYear, 0, 1);
		return ((d.time - firstDay.time) / Microtimes.DAY) + 1;
	}

	//----------------------------------------------------------------
	// clone and move
	//----------------------------------------------------------------
	/** 현재 날짜를 복제한다 */
	public function clone():Day {
		var day:Day=new Day;
		day.d.time=d.time;
		return day;
	}

	/** 내일 */
	public function getNextDay():Day {
		var day:Day=clone();
		day.d.time+=Microtimes.DAY;
		return day;
	}

	/** 어제 */
	public function getPrevDay():Day {
		var day:Day=clone();
		day.d.time-=Microtimes.DAY;
		return day;
	}

	/** 이번달을 첫째날 */
	public function getFirstDayOfMonth():Day {
		var day:Day=clone();
		day.d.date=1;
		return day;
	}

	/** 이번달의 마지막날 */
	public function getLastDayOfMonth():Day {
		var day:Day=clone();
		day.d.date=DateUtils.getLastDay(day.d.fullYear, day.d.month);
		return day;
	}

	/** 이번주의 첫째날 */
	public function getFirstDayOfWeek():Day {
		var day:Day=clone();
		day.d.time-=Microtimes.DAY * day.d.day;
		return day;
	}

	/** 이번주의 마지막날 */
	public function getLastDayOfWeek():Day {
		var day:Day=clone();
		day.d.time+=Microtimes.DAY * (6 - day.d.day);
		return day;
	}

	/** 다음달 */
	public function getNextMonth():Day {
		var day:Day=clone();

		if (day.d.month >= 11) {
			day.d.month=0;
			day.d.fullYear++;
		} else {
			day.d.month++;
		}

		return day;
	}

	/** 지난달 */
	public function getPrevMonth():Day {
		var day:Day=clone();

		if (day.d.month <= 0) {
			day.d.month=11;
			day.d.fullYear--;
		} else {
			day.d.month--;
		}

		return day;
	}

	/** 다음주 */
	public function getNextWeek():Day {
		var day:Day=clone();
		day.d.time+=Microtimes.WEEK;
		return day;
	}

	/** 지난주 */
	public function getPrevWeek():Day {
		var day:Day=clone();
		day.d.time-=Microtimes.WEEK;
		return day;
	}

	/** 내년 */
	public function getNextYeay():Day {
		var day:Day=clone();
		day.d.fullYear+=1;
		return day;
	}

	/** 작년 */
	public function getPrevYeay():Day {
		var day:Day=clone();
		day.d.fullYear-=1;
		return day;
	}

	//----------------------------------------------------------------
	// 
	//----------------------------------------------------------------
	private static const days:Vector.<String>=new <String>["일", "월", "화", "수", "목", "금", "토"];

	public function toString():String {
		return StringUtils.formatToString('[Day Day.as년 {1}월 {2}일 {3}요일]', date.fullYear, date.month + 1, date.date, days[date.day]);
	}
}
}
</code></pre><script src="prism.js" data-default-language="javascript"></script><script src="doc.js"></script></body></html>